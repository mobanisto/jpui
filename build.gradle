wrapper {
    gradleVersion = '7.6.1'
}

buildscript {
    repositories {
        maven { url 'https://mvn.topobyte.de' }
    }
    dependencies {
        classpath 'de.topobyte:gradle-launch-and-install-plugin:0.0.5'
    }
}

apply plugin: 'application'
apply plugin: 'de.topobyte.launch-and-install-gradle-plugin'
apply plugin: 'eclipse'

group = pGroup
version = pVersion

mainClassName = 'org.jpui.JPUI'

startScripts.enabled = false

launchAndInstall {
    installationPath = 'mobanisto/jpui'
    makeselfLabel = 'JPUI'
    createScript(project, mainClassName, 'jpui')
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    maven {
        url 'https://mvn.topobyte.de'
    }
    mavenCentral()
}

dependencies {
    implementation 'de.topobyte:shared-preferences:0.0.1'
    implementation 'de.topobyte:swing-utils:0.0.13'
    testImplementation("junit:junit:4.13.2")
}

task copyJars(type: Copy) {
    into "$buildDir/lib-run"
    from jar
}

task copyDeps(type: Copy) {
    into "$buildDir/lib-run"
    from configurations.runtimeClasspath
}

task createRuntime(dependsOn: [copyJars, copyDeps]) {
    description 'Setup the execution environment for the main scripts'
}
